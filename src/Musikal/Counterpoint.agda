module Musikal.Counterpoint where

open import Musikal.Base
open import Relation.Binary.PropositionalEquality hiding ([_])

private variable
  d : ğ”»
  A B : Set
  p : A
  m n x y : Music A

-- _â–¹â†’âˆ£_ is the same thing as _â–¹_
delayed-par : (x y : Music A) â†’ x â–¹ y â‰¡ x â–¹â†’âˆ£ y
delayed-par x y = begin
  x â–¹ y                      â‰¡âŸ¨ sym (cong (_â–¹ _) (âˆ£-identityÊ³ x (dur x) â‰¤-refl)) âŸ©
  (x âˆ£ ğ„½ (dur x)) â–¹ y        â‰¡âŸ¨ sym (cong (_ â–¹_) (âˆ£-identityË¡ _ 0ğ”» 0ğ”»â‰¤n)) âŸ©
  (x âˆ£ ğ„½ (dur x)) â–¹ (âŠ˜ âˆ£ y)  â‰¡âŸ¨ sym (interchange _ _ _ _ refl) âŸ©
  x â–¹ âŠ˜ âˆ£ ğ„½ (dur x) â–¹ y      â‰¡âŸ¨ cong (_âˆ£ ğ„½ (dur x) â–¹ y) (â–¹-identityÊ³ _) âŸ©
  x âˆ£ ğ„½ (dur x) â–¹ y          âˆ
  where open â‰¡-Reasoning

----

-- Now we'd like to show that any piece of music can be considered as
-- a collection of parallel lines of sequential notes.

-- Seq is capable only of expressing sequential music
data Seq (Embed : Music A â†’ Set) : Music A â†’ Set where
  embed : Embed m â†’ Seq Embed m
  ğ…Ÿ : Seq Embed (ğ…Ÿ p d)
  ğ„½ : Seq Embed (ğ„½ d)
  _â–¹_ : Seq Embed x â†’ Seq Embed y â†’ Seq Embed (x â–¹ y)

-- Par is capable only of expressing parallel music
data Par {A : Set} (Embed : Music A â†’ Set) : Music A â†’ Set where
  embed : Embed m â†’ Par Embed m
  ğ…Ÿ : Par Embed (ğ…Ÿ p d)
  ğ„½ : Par Embed (ğ„½ d)
  _âˆ£_ : Par Embed x â†’ Par Embed y â†’ Par Embed (x âˆ£ y)


open import Data.Empty

-- A helper type to bottom out our embedding (in essence saying the `embed`
-- constructor above cannot be used.)
NoFurtherEmbedding : Music A â†’ Set
NoFurtherEmbedding _ = âŠ¥

-- Parallel lines of sequential music (aka counterpoint)
ParSeq : (A : Set) â†’ Music A â†’ Set
ParSeq A = Par {A = A} (Seq {A = A} NoFurtherEmbedding)

-- Sequences of parallel music (aka sequences of chords)
SeqPar : (A : Set) â†’ Music A â†’ Set
SeqPar A = Seq {A = A} (Par {A = A} NoFurtherEmbedding)

-- Given a ParSeq of m and n, we can give a ParSeq of m â–¹â†’âˆ£ n
_â–¹â†’âˆ£â‚š_ : ParSeq A m â†’ ParSeq A n â†’ ParSeq A (m â–¹â†’âˆ£ n)
m â–¹â†’âˆ£â‚š embed x = m âˆ£ embed (ğ„½ â–¹ x)
m â–¹â†’âˆ£â‚š ğ…Ÿ       = m âˆ£ embed (ğ„½ â–¹ ğ…Ÿ)
m â–¹â†’âˆ£â‚š ğ„½       = m âˆ£ embed (ğ„½ â–¹ ğ„½)
_â–¹â†’âˆ£â‚š_ {A} {m = mm} m (_âˆ£_ {x} {y} nâ‚ nâ‚‚) = subst (ParSeq A)
  ( begin
    let d = ğ„½ (dur mm) in
    (mm âˆ£ d â–¹ x) âˆ£ (mm âˆ£ d â–¹ y)  â‰¡âŸ¨ cong (_âˆ£ (mm âˆ£ d â–¹ y)) (âˆ£-comm _ _) âŸ©
    (d â–¹ x âˆ£ mm) âˆ£ (mm âˆ£ d â–¹ y)  â‰¡âŸ¨ âˆ£-assoc _ _ _ âŸ©
    d â–¹ x âˆ£ (mm âˆ£ (mm âˆ£ d â–¹ y))  â‰¡âŸ¨ cong (d â–¹ x âˆ£_) (sym (âˆ£-assoc _ _ _)) âŸ©
    d â–¹ x âˆ£ (mm âˆ£ mm) âˆ£ d â–¹ y    â‰¡âŸ¨ cong (Î» Ï† â†’ d â–¹ x âˆ£ Ï† âˆ£ d â–¹ y) (âˆ£-idem _) âŸ©
    d â–¹ x âˆ£ (mm âˆ£ d â–¹ y)         â‰¡âŸ¨ sym (âˆ£-assoc _ _ _) âŸ©
    (d â–¹ x âˆ£ mm) âˆ£ d â–¹ y         â‰¡âŸ¨ cong (_âˆ£ d â–¹ y) (âˆ£-comm _ _) âŸ©
    (mm âˆ£ d â–¹ x) âˆ£ d â–¹ y         â‰¡âŸ¨ âˆ£-assoc _ _ _ âŸ©
    mm âˆ£ (d â–¹ x âˆ£ d â–¹ y)         â‰¡âŸ¨ cong (mm âˆ£_) (elim-head _ _ _) âŸ©
    mm âˆ£ d â–¹ (x âˆ£ y)             âˆ
  ) (m â–¹â†’âˆ£â‚š nâ‚ âˆ£ m â–¹â†’âˆ£â‚š nâ‚‚)
  where open â‰¡-Reasoning

-- Any piece of music can be encoded as a parallel sequence:
asLines : (m : Music A) â†’ ParSeq A m
asLines (ğ…Ÿ p d) = ğ…Ÿ
asLines (ğ„½ d) = ğ„½
asLines {A} (m â–¹ n) = subst (ParSeq A) (sym (delayed-par _ _)) (asLines m â–¹â†’âˆ£â‚š asLines n)
asLines (m âˆ£ n) = asLines m âˆ£ asLines n

open import Data.List

linesFromParSeq : ParSeq A m â†’ List (Music A)
linesFromParSeq {m = m} (embed x) = [ m ]
linesFromParSeq {m = m} ğ…Ÿ = [ m ]
linesFromParSeq {m = m} ğ„½ = [ m ]
linesFromParSeq (x âˆ£ y) = linesFromParSeq x ++ linesFromParSeq y

lines : Music A â†’ List (Music A)
lines m = linesFromParSeq (asLines m)


open import Relation.Nullary
open import Data.Unit

-- However, not all music can be encoded as sequential parallel notes:
Â¬asChords : Â¬ ((A : Set) â†’ (m : Music A) â†’ SeqPar A m)
Â¬asChords f with f âŠ¤ (ğ…Ÿ tt 1ğ”» â–¹ ğ…Ÿ tt 2ğ”» âˆ£ ğ…Ÿ tt 2ğ”» â–¹ ğ…Ÿ tt 1ğ”»)
... | embed (embed () âˆ£ _)

-- Therefore, we are justified in decomposing music into counterpoint, but NOT
-- into sequences of chords.

